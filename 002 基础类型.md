# 基础类型
在这章我们将学习更多关于 Elixir 的基础类型的知识：整数，浮点数，布尔值，原子，字符串，列表和元组。这些基础类型是：

```
iex> 1          # integer
iex> 0x1F       # integer
iex> 1.0        # float
iex> true       # boolean
iex> :atom      # atom / symbol
iex> "elixir"   # string
iex> [1, 2, 3]  # list
iex> {1, 2, 3}  # tuple
```

## 基础运算
打开 iex 并输入下面的表达式：

```
iex> 1 + 2
3
iex> 5 * 5
25
iex> 10 / 2
5.0
```

注意 10/2 返回的是一个浮点数 5.0 而不是整数 5。在 Elixir 里， 操作符 / 总是返回一个浮点数。如果你想取余或者做一个整数除法运算，你可以调用 rem 和 div 函数：

```
iex> div(10, 2)
5
iex> div 10, 2
5
iex> rem 10, 3
1
```

注意在调用函数时，括号是不需要的。

Elixir 同样像其他语言一样输入二进制，八进制和十六进制数值：

```
iex> 0b1010
10
iex> 0o777
511
iex> 0x1F
31
```

浮点数在点后面，需要至少跟随一个数字，同时支持用 e 表达指数：

```
iex> 1.0
1.0
iex> 1.0e-10
1.0e-10
```

浮点数在 Elixir 是64位双精度。

你可以调用 round 函数对浮点进行四舍五入，或者使用 trunc 函数获取整数部分。

```
iex> round 3.58
4
iex> trunc 3.58
3
```

## 布尔值

Elixir 支持 `true` 和 `false` 布尔值:

    iex> true
    true
    iex> true == false
    false

Elixir 提供一系列函数来检测值的类型。例如 `is_boolean/1` 可以用来检测一个值是否是布尔值：

>注意: 函数在 Elixir 中用名称和参数的数量来定义。因此 `is_boolean/1` 定义一个名称为 is_boolean 需要一个参数的函数. `is_boolean/2` （这个函数事实上没有）定义一个同样名称但参数数量不同的函数。

    iex> is_boolean(true)
    true
    iex> is_boolean(1)
    false

你也可以用 `is_integer/1`, `is_float/1` 或 `is_number/1` 分别测试它们的参数是整数还是浮点数。

>注意：在任何情况你都可以在 shell 里通过输入 h 来打印如何使用 shell 的帮助信息。h 助手同样可以用来访问文档的任何参数。例如输入 `h is_integer/1` 将打印函数 `is_integer/1` 的相关文档。这种方式同样适合操作符号和其他构造。例如：`try h ==/2`

## 原子
原子是它的名称是它值的常量。有些语言把它叫做符号：

    iex> :hello
    :hello
    iex> :hello == :world
    false
    
布尔值的 true 和 false 实际上就是原子：

    iex> true == :true
    true
    iex> is_atom(false)
    true
    iex> is_boolean(:false)
    true

## 字符串

字符串在 Elixir 里必须插入双引号中，并且它们使用 UTF-8 编码：

    iex> "hellö"
    "hellö"

>注意：如果你运行的系统是 Windows，你的终端使用的可能不是 UTF-8 编码。你可以在进入 IEx 之前使用 `chcp 65001` 改变你当前的会话的编码。

Elixir 同样支持字符串插值：

    iex> "hellö #{:world}"
    "hellö world"

字符串可以使用换行符或者逃逸序列：

    iex> "hello
    ...> world"
    "hello\nworld"
    iex> "hello\nworld"
    "hello\nworld"
    
你可以使用函数 IO 模块的 `IO.puts/1` 函数打印字符串：

    iex> IO.puts "hello\nworld"
    hello
    world
    :ok
    
>注意：`IO.puts/1` 函数在打印后会返回一个 `:ok` 原子。

字符串在 Elixir 内部使用二进制的字节序列表示：

    iex> is_binary("hellö")
    true
    
我们也可以获取字符串的字节长度：

    iex> byte_size("hellö")
    6

注意返回数值是 6，而不是5。这是因为 “ö” 在 UTF-8 下的字节长度为 2。我们可以用函数 `String.length/1` 获取字符串的长度：

    iex> String.length("hellö")
    5

字符串模块有一些列，按照 Unicode 标准操作字符串的函数：

    iex> String.upcase("hellö")
    "HELLÖ"

## 匿名函数

函数使用关键字 fn 和 end 鉴定：

    iex> add = fn a, b -> a + b end
    #Function<12.71889879/2 in :erl_eval.expr/5>
    iex> is_function(add)
    true
    iex> is_function(add, 2)
    true
    iex> is_function(add, 1)
    false
    iex> add.(1, 2)
    3
    
函数在 Elixir 里是 “一等公民”，这意味着它们可以像数值和字符串一样，可以当做参数传递给其他函数。比如说，我们把一个函数当做参数传递给 `is_function/1` 来测试它是不是一个函数值。我们还可以用 `is_function/2` 来获取函数参数的数量。

注意在变量和括号之间的点 (.) 在调用匿名函数时是必须的。

匿名函数都是闭包的，它们可以访问在定义时的作用域的变量：

    iex> add_two = fn a -> add.(a, 2) end
    #Function<6.71889879/1 in :erl_eval.expr/5>
    iex> add_two.(2)
    4

记住在函数内部内部分配变量不影响周围环境：

    iex> x = 42
    42
    iex> (fn -> x = 0 end).()
    0
    iex> x
    42
## (链接) 列表

Elixir 使用方括号指定类表的值，值可以是任意类型：

    iex> [1, 2, true, 3]
    [1, 2, true, 3]
    iex> length [1, 2, 3]
    3
列表可以使用`++/2` 或 `--/2` 操作符连接或减除：

    iex> [1, 2, 3] ++ [4, 5, 6]
    [1, 2, 3, 4, 5, 6]
    iex> [1, true, 2, false, 3, true] -- [true, false]
    [1, 2, 3, true]
`hd/1` 函数返回列表的第一个函数，`tl/1` 函数返回其余部分：

    iex> list = [1,2,3]
    iex> hd(list)
    1
    iex> tl(list)
    [2, 3]
获取一个空列表的头或者尾会抛出错误：

    iex> hd []
    ** (ArgumentError) argument error
有时候，如果你创建一个列表会返回单引号的值：

    iex> [11, 12, 13]
    '\v\f\r'
    iex> [104, 101, 108, 108, 111]
    'hello'
当 Elixir 把列表看做可打印的 ASCII 数字, Elixir 会打印一个字符列表。 当面对 Erlang 的退出代码时，字符列表非常普通。

请记住，在 Elixir 里，单引号和双引号表达的值并不是一个类型：

    iex> 'hello' == "hello"
    false
单引号的是字符列表，双引号的是字符串。

## 元组
Elixir 使用大括号定义元组。类似列表，元组可以存放任何值：

    iex> {:ok, "hello"}
    {:ok, "hello"}
    iex> tuple_size {:ok, "hello"}
    2
元组保存在内存中连续的元素。这意味着用索引访问元组的元素，或者获取元组的尺寸是高性能的操作（索引从 0 开始）：

    iex> tuple = {:ok, "hello"}
    {:ok, "hello"}
    iex> elem(tuple, 1)
    "hello"
    iex> tuple_size(tuple)
    2
还可以使用 `put_elem/3` 方法在指定位置插入元素：

    iex> tuple = {:ok, "hello"}
    {:ok, "hello"}
    iex> put_elem(tuple, 1, "world")
    {:ok, "world"}
    iex> tuple
    {:ok, "hello"}
注意 `put_elem/3` 返回一个新元组。 原来保存在变量 tuple 中的元组不会被改变，因为在 Elixir 中数据类型是不可变的。也正是因为这种特性，在 Elixir 中不用担心某个特别的代码会意外的改变数据结构。

也因为不能改变的特性， Elixir 同样能够避免在并发时出现，在同一时间两个实体尝试修改一个数据结构的抢占情况。


## 列表还是元组
列表和元组有什么不同？

列表是以链接列表的方式保存在内存中，这意味着在列表中的每一个元素，直到结束，都保存了相应的值和指向下一个元素标记。我们把每一对值和指针叫做 `cons cell`：

    iex> list = [1|[2|[3|[]]]]
    [1, 2, 3]
所以访问列表的长度是一个线性操作：我们需要遍历整个列表来获取它的长度。如果只是在列表前端插入元素，更新列表的操作非常快：

    iex> [0 | list]
    [0, 1, 2, 3]
元组，是连续保存在内存中的数据。这意外着获取元组的长度或者使用索引访问元素的速度非常快。但是，更新或者添加元组的元素更占用资源，因为它必须拷贝在内存中的整个元组。

这些性能上的特性，决定了它们的应用场景。元组的一种常用方式是，用来在函数中返回额外的信息。例如，`File.read/1` 是一个用来读取文件内容并返回一个元组的函数：

    iex> File.read("path/to/existing/file")
    {:ok, "... contents ..."}
    iex> File.read("path/to/unknown/file")
    {:error, :enoent}
如果一个给定的路径的文件存在，它返回的元组包括原子 `:ok` 和文件的内容。否则将返回 `:error` 和错误的描述。

在大多数情况下， Elixir 会引导你做正确的事情。例如， `elem/2` 函数可以用来访问元组的元素，但操作列表的时候，没有这样的方法：

    iex> tuple = {:ok, "hello"}
    {:ok, "hello"}
    iex> elem(tuple, 1)
    "hello"
当计算一个数据结构的元素数量时， Elixir 遵循一个简单的原则：当一个计数函数的名称包括 `size`时它的效率很快，`length` 时是一个线性操作。

例如，我们已经使用过 4 个计数函数：`byte_size/1`（获取字符串字节的长度），`tuple_size/1`（获取元组的尺寸），`length/1`（获取列表的长度），`String.length/1`（获取字符串的字符数量）。这就是说用 `byte_size/1` 获取字符串的字节数，效率很高，但是获取字符数量就慢很多，因为必须遍历整个字符串。
