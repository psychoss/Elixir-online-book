# 模式匹配
在这一章里，我们将展示在  Elixir 里 `=` 操作符实际上是一个匹配操作符，再者，如何使用它对数据结构执行模式匹配。最后，我们将学习怎么使用 `^` 操作符号访问变量之前绑定的值。

## 匹配操作符

我们已经多次使用 `=` 操作符在 Elixir 给变量分配值：

    iex> x = 1
    1
    iex> x
    1
在 Elixir中 `=` 操作符其实就叫做匹配操作符：

    iex> 1 = x
    1
    iex> 2 = x
    ** (MatchError) no match of right hand side value: 1
注意 `1 = x` 是一个合法的表达式，因为左右两边的值都等于1.如果两边不匹配，将抛出一个 MatchError 错误。

变量尽在 `=` 的左边才能被分配值：

    iex> 1 = unknown
    ** (RuntimeError) undefined function: unknown/0
因为在这里没有定义一个叫做 unknown的变量。Elixir 认为你是想尝试调用函数 `unknown/0` 但这个函数实际上不存在。

## 模式匹配

匹配操作符不仅仅用于匹配单一的值，但它也可以用来解构复杂的数据类型。例如，我们可以在元组上使用模式匹配：

    iex> {a, b, c} = {:hello, "world", 42}
    {:hello, "world", 42}
    iex> a
    :hello
    iex> b
    "world"
当两边无法匹配时，将会抛出一个错误。例如，下面这两个尺寸不同的元组：

    iex> {a, b, c} = {:hello, "world"}
    ** (MatchError) no match of right hand side value: {:hello, "world"}
当比较不同类型时也是如此：

    iex> {a, b, c} = [:hello, "world", "!"]
    ** (MatchError) no match of right hand side value: [:hello, "world", "!"]
更有趣的是，我们可以匹配特殊的值。下面的例子申明只有在右边元组的第一个元素是 `:ok` 时，才能匹配：

    iex> {:ok, result} = {:ok, 13}
    {:ok, 13}
    iex> result
    13

    iex> {:ok, result} = {:error, :oops}
    ** (MatchError) no match of right hand side value: {:error, :oops}
我们还可以在列表上使用匹配：

    iex> [a, b, c] = [1, 2, 3]
    [1, 2, 3]
    iex> a
    1
列表同样支持匹配它的开头和结尾：

    iex> [head | tail] = [1, 2, 3]
    [1, 2, 3]
    iex> head
    1
    iex> tail
    [2, 3]
这和使用 `hd/1` 和 `tl/1`函数一样，我们不能在空列表上使用头尾模式匹配：

    iex> [h|t] = []
    ** (MatchError) no match of right hand side value: []
The [head | tail] format is not only used on pattern matching but also for prepending items to a list:

    iex> list = [1, 2, 3]
    [1, 2, 3]
    iex> [0|list]
    [0, 1, 2, 3]
模式匹配允许开发者轻松的结构像列表这样的数据类型。我们将在以后的章节里，它是递归的基础， Elixir 同样应用到其他的类型，如 map 和二进制等。

## ^ 操作符

变量在 Elixir 中可以重新绑定：

    iex> x = 1
    1
    iex> x = 2
    2
在想要匹配一个存在的变量的值，而不是重新绑定的值时，应该使用`^` 操作符：

    iex> x = 1
    1
    iex> ^x = 2
    ** (MatchError) no match of right hand side value: 2
    iex> {y, ^x} = {2, 1}
    {2, 1}
    iex> y
    2
    iex> {y, ^x} = {2, 2}
    ** (MatchError) no match of right hand side value: {2, 2}
Because we have assigned the value of 1 to the variable x, this last example could also have been written as:

    iex> {y, 1} = {2, 2}
    ** (MatchError) no match of right hand side value: {2, 2}
Notice that if a variable is mentioned more than once in a pattern, all references should bind to the same pattern:

    iex> {x, x} = {1, 1}
    1
    iex> {x, x} = {1, 2}
    ** (MatchError) no match of right hand side value: {1, 2}
在一些实例里，你并不关心模式里的某些特别的值。这时候可以使用 `_` 下划线替代它们。比如说，如果你只需要列表的头：

    iex> [h | _] = [1, 2, 3]
    [1, 2, 3]
    iex> h
    1
`_` 变量是一个特殊的变量，它不能被读取。如果尝试读取它的值，会抛出一个未绑定的变量的错误：

    iex> _
    ** (CompileError) iex:1: unbound variable _
虽然模式匹配允许我们创建强大结构，它的使用舞台是有限制的。例如，我们不能进一步对左边的匹配调用函数，下面的实例是不允许的：

    iex> length([1,[2],3]) = 3
    ** (CompileError) iex:1: illegal pattern
